http://its.1c.ru/db/metod8dev#content:5011:hdoc:_top:%D0%B0%D0%B2%D1%82%D0%BE%D0%BC%D0%B0%D1%82%D0%B8%D0%B7%D0%B8%D1%80%D0%BE%D0%B2%D0%B0%D0%BD%D0%BD%D0%BE%D0%B5%20%D1%82%D0%B5%D1%81%D1%82%D0%B8%D1%80%D0%BE%D0%B2%D0%B0%D0%BD%D0%B8%D0%B5

"C:\Program Files (x86)\1cv8\8.3.8.2197\bin\1cv8s.exe" ENTERPRISE /F c:\Base1C\Platform83Demo\ /N Администратор /TESTMANAGER
"C:\Program Files (x86)\1cv8\8.3.8.2197\bin\1cv8s.exe" ENTERPRISE /F c:\Base1C\Platform83Demo\ /N Администратор /TESTCLIENT


// Примера кода для работы менджера тестирования которые он будет выполнять в интерфесе тест клиента.
//----------------------------------
ЗапуститьСистему(
"C:\Program Files (x86)\1cv8\8.3.8.2197\bin\1cv8s.exe ENTERPRISE /F c:\Base1C\Platform83Demo\ /N Администратор /TESTCLIENT");
//----------------------------------
ТестКлиент = Новый ТестируемоеПриложение();
//----------------------------------
МаксИтераций = 60;
Для Счетчик = 1 По МаксИтераций Цикл

   Попытка
      ТестКлиент.УстановитьСоединение();
      Прервать; 

   Исключение

      // Вывести только последнее сообщение.
      Если Счетчик = МаксИтераций Тогда
         Сообщить(ОписаниеОшибки());
         Возврат; 

      КонецЕсли; 

   КонецПопытки; 

КонецЦикла; 
//----------------------------------
ГлавноеОкноТестКлиента = ТестКлиент.НайтиОбъект(Тип("ТестируемоеОкноКлиентскогоПриложения"), "Демонстрационная конфигурация*");
//----------------------------------
ГлавноеОкноТестКлиента.ВыполнитьКоманду("e1cib/navigationpoint/ТоварныеЗапасы");
//----------------------------------
ГлавноеОкноТестКлиента.ВыполнитьКоманду("e1cib/navigationpoint/ТоварныеЗапасы/Справочник.Товары.Команда.ОткрытьСписок");
//----------------------------------
Если НЕ ТестКлиент.ОжидатьОтображениеОбъекта(Тип("ТестируемаяФорма"), "Товары*") Тогда
   Сообщить("Не удалось открыть форму списка справочника ""Товары"" в течение 60 секунд.");
   Возврат;

КонецЕсли;
//----------------------------------

ФормаТоваров = ТестКлиент.НайтиОбъект(Тип("ТестируемаяФорма"), "Товары*");
//----------------------------------
Это стандартная команда, поэтому ее навигационная ссылка формируется по принципу: "e1cib/command/" + <имя стандартной команды>.

Ожидаем отображение формы нового товара и получаем её:

Если НЕ ТестКлиент.ОжидатьОтображениеОбъекта(Тип("ТестируемаяФорма"), "Товар*") Тогда
   Сообщить("Не удалось открыть форму нового товара в течение 60 секунд.");
   Возврат;

КонецЕсли;

ФормаНовогоТовара = ТестКлиент.НайтиОбъект(Тип("ТестируемаяФорма"), "Товар*");

Так выглядит полученная нами форма:



Заполним поле Наименование. После открытия формы это поле активно, поэтому просто находим его и вводим текст:

ПолеНаименование = ФормаНовогоТовара.НайтиОбъект(Тип("ТестируемоеПолеФормы"), "Наименование");
ПолеНаименование.ВвестиТекст("Новый товар (автотест)");

Заполним поле Артикул. Это поле неактивно, поэтому перед вводом текста его нужно активизировать:

ПолеАртикул = ФормаНовогоТовара.НайтиОбъект(Тип("ТестируемоеПолеФормы"), "Артикул*");
ПолеАртикул.Активизировать();
ПолеАртикул.ВвестиТекст("ДлинныйАртикулТовара");

Нажмём кнопку Записать и Закрыть. Идентифицируем её по заголовку:

КнопкаЗаписатьИЗакрыть = ФормаНовогоТовара.НайтиОбъект(Тип("ТестируемаяКнопкаФормы"), "Записать и закрыть");
КнопкаЗаписатьИЗакрыть.Нажать();

На этом выполнение теста закончено.
Проверка результатов теста

Проверять результаты тестирования будем так же, как это делает пользователь визуально. В списке товаров найдём наш новый товар по наименованию. Откроем его и проверим значения полей Наименование и Артикул.

Во-первых, нашего товара может и не быть в списке, если запись товара по каким-то причинам не была выполнена. Например, если в тесте закомментировать ввод наименования, то новый товар не будет записан, так как это поле обязательное для заполнения.

Во-вторых, записанные значения полей могут отличаться от тех, которые мы устанавливали. Например, длина поля Артикул - 15 символов. И наверняка введенный нами артикул не будет записан целиком.

Форма списка товаров у нас открыта, поэтому получим таблицу из этой формы, "встанем" на её начало и будем спускаться вниз в надежде найти наш товар.

СписокТоваров = ФормаТоваров.НайтиОбъект(Тип("ТестируемаяТаблицаФормы"), "Список*");
СписокТоваров.ПерейтиКПервойСтроке();

Обход таблицы будем выполнять в бесконечном цикле. Получая текст ячейки Наименование из текущей строки и сравнивая его с эталонным наименованием.

В случае удачи прерываем цикл. А в случае неудачи пытаемся перейти к следующей строке. Если следующей строки нет - выходим из цикла:

ТестируемыйТоварНайден = Ложь;
Пока Истина Цикл
   НаименованиеТовара = СписокТоваров.ПолучитьТекстЯчейки("Наименование");

   // Это искомый товар - выходим из цикла.
   Если НаименованиеТовара = "Новый товар (автотест)" Тогда
      ТестируемыйТоварНайден = Истина;
      Прервать;

   КонецЕсли;	

   // Это не тот товар - переходим к следующей строке, если она есть.
   Если НЕ НаименованиеТовара = "Новый товар (автотест)" Тогда

      // Если следующей строки нет, выходим из цикла.
      Если НЕ СписокТоваров.ПерейтиКСледующейСтроке() Тогда
         Прервать;

      КонецЕсли;

   КонецЕсли;	

КонецЦикла;


// Тестируемый товар не найден - ошибка теста.
Если Не ТестируемыйТоварНайден Тогда
   Сообщить("Тестируемый товар не найден в списке товаров. Не выполнена запись тестируемого товара.");
   Возврат;


КонецЕсли;

После того, как тестируемый товар найден, открываем его форму, имитируя интерактивное действие пользователя - двойной щелчок мыши или нажатие клавиши Enter:

СписокТоваров.Выбрать();

"Как обычно" ожидаем открытия формы и получаем её:

Если НЕ ТестКлиент.ОжидатьОтображениеОбъекта(Тип("ТестируемаяФорма"), "Товар*") Тогда
   Сообщить("Не удалось открыть форму тестируемого товара в течение 60 секунд.");
   Возврат;
КонецЕсли;

ФормаТестируемогоТовара = ТестКлиент.НайтиОбъект(Тип("ТестируемаяФорма"), "Новый товар (автотест)*");

Находим поле Артикул и получаем его значение:

ПолеАртикул = ФормаТестируемогоТовара.НайтиОбъект(Тип("ТестируемоеПолеФормы"), "Артикул");
ТекущийАртикул = ПолеАртикул.ПолучитьПредставлениеДанных();

Если его значение отличается от эталона - прекращаем тестирование (или просто сообщаем об ошибке):

Если ТекущийАртикул <> "ДлинныйАртикулТовара" Тогда
   Сообщить("Ошибка в поле ""Артикул"": Эталон: ДлинныйАртикулТовара , Текущее значение: " + ТекущийАртикул);
   Возврат;
КонецЕсли;

Аналогично можно проверить и значения других полей формы, например поля Наименование.